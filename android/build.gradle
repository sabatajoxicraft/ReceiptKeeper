buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 24
        compileSdkVersion = 34
        targetSdkVersion = 34
        ndkVersion = "26.1.10909125"
        kotlinVersion = "1.9.22"
        reactNativeVersion = "0.73.6"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.3.0")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    }
}

allprojects {
    repositories {
        // Already defined in settings.gradle with dependencyResolutionManagement
    }
    configurations.all {
        resolutionStrategy {
            force "com.facebook.react:react-android:0.73.6"
            force "com.facebook.react:hermes-android:0.73.6"
        }
        resolutionStrategy.dependencySubstitution {
            // React Native 0.71+ renamed the artifact from react-native to react-android
            substitute module("com.facebook.react:react-native") using module("com.facebook.react:react-android:0.73.6")
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                if (it.hasProperty('compileOptions')) {
                    compileOptions {
                        sourceCompatibility JavaVersion.VERSION_17
                        targetCompatibility JavaVersion.VERSION_17
                    }
                }
                // Enable buildConfig for all subprojects (required by lottie-react-native)
                if (it.hasProperty('buildFeatures')) {
                    buildFeatures {
                        buildConfig = true
                    }
                }
            }
        }
        if (project.hasProperty('kotlin')) {
            tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                kotlinOptions {
                    jvmTarget = "17"
                }
            }
        }
    }
}
